/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'simon' at '30.06.16 23:30' with Gradle 2.14
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.14/userguide/tutorial_java_projects.html
 */

plugins {
  id "ca.coglinc.javacc" version "2.3.1"
  id "com.jfrog.bintray" version "1.7"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'


version = '0.7.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    javaccOutput = file(project.buildDir.absolutePath + '/generated/javacc/ch/poole/poparser')
    mainSource = file('src/main/java/ch/poole/poparser')
    testSource = file('src/test/java/ch/poole/poparser')
}

compileJavacc {
    inputDirectory = mainSource
    outputDirectory = javaccOutput
}

compileJava {
    source javaccOutput,mainSource
}

compileTestJava {
    source javaccOutput,mainSource,testSource
}

javadoc {
    source mainSource, testSource
    exclude '**/*.jj'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'ch.poole'
            artifactId 'PoParser'
            version '0.7.0'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'div'
        name = 'PoParser'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/simonpoole/PoParser.git'
        version {
        	name = '0.7.0'
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
	testCompile 'junit:junit:4.12'
}
